apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def navVersion = "2.3.0-alpha01"
def daggerVersion = '2.16'
def moxyVersion = '2.0.2'
def roomVersion = '2.2.3'
def glideVersion = '4.9.0'
def circleImageViewVersion = '2.2.0'
def rxJavaVersion = '2.2.2'
def rxAndroidVersion = '2.0.2'
def stethoVersion = '1.5.0'
def stethoLeakCanaryVersion = '1.5.4'
def easyCryptVersion = '1.3.3'

def rootExtConfiguration = rootProject.extensions.getByName("ext")

String VERSION_NAME = rootExtConfiguration["appVersionName"]
int VERSION_CODE = rootExtConfiguration["appVersionCode"]

int COMPILE_SDK_VERSION = rootExtConfiguration["compileSdkVersion"]
int MIN_SDK_VERSION = rootExtConfiguration["minSdkVersion"]
int TARGET_SDK_VERSION = rootExtConfiguration["targetSdkVersion"]

android {
    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        applicationId "com.ak.passwordsaver"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        disableEmptyStrategyCheck: 'true',
                        enableEmptyStrategyHelper: 'false',
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            versionNameSuffix 'release'

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "int", "DATA_BASE_VERSION", DATA_BASE_VERSION
            buildConfigField "String", "DATA_BASE_NAME", DATA_BASE_NAME
            buildConfigField "String", "FULL_VERSION_NAME", "\"v. " + VERSION_NAME + "\""
        }
        debug {
            minifyEnabled false
            debuggable true
            versionNameSuffix 'debug'

            buildConfigField "int", "DATA_BASE_VERSION", DATA_BASE_VERSION
            buildConfigField "String", "DATA_BASE_NAME", DATA_BASE_NAME
            buildConfigField "String", "FULL_VERSION_NAME", "\"v. " + VERSION_NAME + "-debug\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PasswordSaver_${VERSION_NAME}_${variant.buildType.name}.apk"
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Dagger 2
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Moxy 2
    implementation "com.github.moxy-community:moxy:$moxyVersion"
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Circle image view
    implementation "de.hdodenhof:circleimageview:$circleImageViewVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Recycler and card views
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // Stetho
    debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$stethoLeakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$stethoLeakCanaryVersion"

    // EasyCrypt
    implementation "com.pvryan.easycrypt:easycrypt:$easyCryptVersion"

    // Permissions
    implementation 'com.sagar:coroutinespermission:1.0.0'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"

    // lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // GSON
    implementation "com.google.code.gson:gson:2.8.6"

    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
}
