apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def rootExtConfiguration = rootProject.extensions.getByName("ext")

String VERSION_NAME = rootExtConfiguration["appVersionName"]
int VERSION_CODE = rootExtConfiguration["appVersionCode"]

int COMPILE_SDK_VERSION = rootExtConfiguration["compileSdkVersion"]
int MIN_SDK_VERSION = rootExtConfiguration["minSdkVersion"]
int TARGET_SDK_VERSION = rootExtConfiguration["targetSdkVersion"]

android {
    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        applicationId "com.ak.passwordsaver"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        disableEmptyStrategyCheck: 'true',
                        defaultMoxyStrategy      : 'moxy.viewstate.strategy.OneExecutionStateStrategy',
                        enableEmptyStrategyHelper: 'false',
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            versionNameSuffix 'release'

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "FULL_VERSION_NAME", "\"v. " + VERSION_NAME + "\""
        }
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            debuggable true
            versionNameSuffix 'debug'

            buildConfigField "String", "FULL_VERSION_NAME", "\"v. " + VERSION_NAME + "-debug\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "PasswordSaver_${VERSION_NAME}_${variant.buildType.name}.apk"
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':base')
    implementation project(':core-repo-api')
    implementation project(':core-repo-impl')
    implementation project(':feature-security-api')
    implementation project(':feature-security-impl')
    implementation project(':feature-tabpasswords-api')
    implementation project(':feature-tabpasswords-impl')
    implementation project(':feature-tabsettings-api')
    implementation project(':feature-tabsettings-impl')
    implementation project(':feature-encryption-api')
    implementation project(':feature-encryption-impl')

    implementation deps.kotlin
    implementation deps.appCompat
    implementation deps.androidX
    implementation deps.androidMaterial
    implementation deps.constraint

    implementation deps.dagger
    kapt deps.daggerKapt

    implementation deps.moxy
    kapt deps.moxyKapt

    implementation deps.rx
    implementation deps.rxAndroid

    debugImplementation deps.stethoDebugImpl

    implementation deps.lifecycle
    implementation deps.gson
    implementation deps.navComponent
}
